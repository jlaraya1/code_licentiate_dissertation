;{CR10X}

;Programa : ESTANDAR PARA ESTACION CLIMATICA TERRENA


;COMENTARIOS QUE INCLUYA LOS SENSORES PROGRAMADOS CON SUS RESPECTIVOS MULTIPLICADORES
;Y OFFSETS


;  Realizado por Jose Luis Araya Lopez.


*Table 1 Program
  01: 10        Execution Interval (seconds)


; ********** SENSOR DE TEMPERATURA *******

1:  Volt (SE) (P1)
 1: 1        Reps
 2: 25       2500 mV 60 Hz Rejection Range
 3: 1        SE Channel
 4: 9        Loc [ TEMP_C    ]
 5: 0.1      Mult
 6: -40      Offset

; ********** SENSOR DE HUMEDAD *********

2:  Volt (SE) (P1)
 1: 1        Reps
 2: 25       2500 mV 60 Hz Rejection Range
 3: 2        SE Channel
 4: 10       Loc [ HR        ]
 5: .1       Mult
 6: 0        Offset


; *******  SENSOR DE RADIACION: **********


3:  Volt (Diff) (P2)
 1: 1        Reps
 2: 3        25 mV Slow Range
 3: 2        DIFF Channel
 4: 12       Loc [ RAD_KJ_m2 ]
 5: 2        Mult
 6: 0        Offset

;**** CORRECCION DE VALORES NEGATIVOS DE RADIACION: *****

4:  If (X<=>F) (P89)
 1: 12       X Loc [ RAD_KJ_m2 ]
 2: 4        <
 3: 0        F
 4: 30       Then Do

     5:  Z=F x 10^n (P30)
      1: 0        F
      2: 0        n, Exponent of 10
      3: 12       Z Loc [ RAD_KJ_m2 ]

6:  End (P95)

; Instruccion 89 es utilizada para anular valores de radiacion
; horarios entre las 7:00 pm inclusive y las 4:00am inclusive

7:  Time (P18)
 1: 1        Minutes into current day (maximum 1440)
 2: 0        Mod/By
 3: 62       Loc [ TIME_NUM  ]

8:  If (X<=>F) (P89)
 1: 62       X Loc [ TIME_NUM  ]
 2: 4        <
 3: 300      F
 4: 30       Then Do

9:  Z=F x 10^n (P30)
 1: 0        F
 2: 0        n, Exponent of 10
 3: 12       Z Loc [ RAD_KJ_m2 ]

10:  End (P95)

11:  If (X<=>F) (P89)
 1: 62       X Loc [ TIME_NUM  ]
 2: 3        >=
 3: 1080     F
 4: 30       Then Do

12:  Z=F x 10^n (P30)
 1: 0        F
 2: 0        n, Exponent of 10
 3: 12       Z Loc [ RAD_KJ_m2 ]

13:  End (P95)

;**** CONVERSION DE KJ/m2 A KW/m2  ***********

14:  Z=X*F (P37)
 1: 12       X Loc [ RAD_KJ_m2 ]
 2: 0.1      F
 3: 11       Z Loc [ RAD_KW_m2 ]

;**** CONVERSION DE KJ/m2 A MJ/m2  ***********

15:  Z=X*F (P37)
 1: 12       X Loc [ RAD_KJ_m2 ]
 2: 0.001    F
 3: 13       Z Loc [ RAD_MJ_m2 ]

16:  If (X<=>F) (P89)
 1: 11       X Loc [ RAD_KW_m2 ]
 2: 3        >=
 3: .12      F
 4: 30       Then Do

;** Casos de hora sol**********************

     17:  Z=Z+1 (P32)
      1: 66       Z Loc [ CASOS_sol ]

18:  End (P95)

; ******** SENSOR DE VELOCIDAD DE VIENTO *****

19:  Pulse (P3)
 1: 1        Reps
 2: 2        Pulse Channel 2
 3: 21       Low Level AC, Output Hz
 4: 14       Loc [ VEL_m_s   ]
 5: .75      Mult
 6: .143     Offset

; ******** SENSOR DE DIRECCION DE VIENTO *****

20:  Excite-Delay (SE) (P4)
 1: 1        Reps
 2: 5        2500 mV Slow Range
 3: 5        SE Channel
 4: 3        Excite all reps w/Exchan 3
 5: 2        Delay (0.01 sec units)
 6: 2500     mV Excitation
 7: 16       Loc [ DIR_vient ]
 8: .1408    Mult
 9: 0        Offset

;***CORRECCION DE VELOCIDAD DEL VIENTO****************

21:  If (X<=>F) (P89)
 1: 14       X Loc [ VEL_m_s   ]
 2: 4        <
 3: .521     F
 4: 30       Then Do

     22:  Z=F x 10^n (P30)
      1: 0        F
      2: 0        n, Exponent of 10
      3: 15       Z Loc [ VCORR_m_s ]

23:  Else (P94)

     24:  Z=X (P31)
      1: 14       X Loc [ VEL_m_s   ]
      2: 15       Z Loc [ VCORR_m_s ]

25:  End (P95)

26:  Volt (SE) (P1)
 1: 1        Reps
 2: 5        2500 mV Slow Range
 3: 6        SE Channel
 4: 64       Loc [ HR_caja   ]
 5: 0.1      Mult
 6: 0        Offset

27:  Internal Temperature (P17)
 1: 18       Loc [ TEM_LOG   ]

;***** VOLTAJE DE LA BATERIA ******

28:  Batt Voltage (P10)
 1: 19       Loc [ VOLT_BAT  ]

;**** Numero de la estacion ***

29:  Z=F x 10^n (P30)
 1: 72149    F
 2: 0        n, Exponent of 10
 3: 20       Z Loc [ ESTACION  ]

30:  Z=F x 10^n (P30)
 1: -8       F
 2: 0        n, Exponent of 10
 3: 65       Z Loc [ SENS_AUS  ]

31:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 1        Interval (same units as above)
 3: 30       Then Do

32:  Block Move (P54)
 1: 29       No. of Values
 2: 22       First Source Loc [ LLUV_2mi  ]
 3: 1        Source Step
 4: 21       First Destination Loc [ LLUV_1mi  ]
 5: 1        Destination Step

33:  End (P95)

34:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 1        Interval (same units as above)
 3: 10       Set Output Flag High (Flag 0)

35:  Set Active Storage Area (P80)
 1: 3        Input Storage Area
 2: 50       Loc [ LLUV_30mi ]

36:  Totalize (P72)^11551
 1: 1        Reps
 2: 17       Loc [ LLUV_mm   ]

37:  Spatial Average (P51)
 1: 5        Swath
 2: 46       First Loc [ LLUV_26mi ]
 3: 51       Avg Loc [ PRO_5mi   ]

38:  Z=X*F (P37)
 1: 51       X Loc [ PRO_5mi   ]
 2: 5        F
 3: 55       Z Loc [ TOT_5mi   ]

39:  Spatial Average (P51)
 1: 10       Swath
 2: 41       First Loc [ LLUV_21mi ]
 3: 52       Avg Loc [ PRO_10mi  ]

40:  Z=X*F (P37)
 1: 52       X Loc [ PRO_10mi  ]
 2: 10       F
 3: 56       Z Loc [ TOT_10mi  ]

41:  Spatial Average (P51)
 1: 15       Swath
 2: 36       First Loc [ LLUV_16mi ]
 3: 53       Avg Loc [ PRO_15mi  ]

42:  Z=X*F (P37)
 1: 53       X Loc [ PRO_15mi  ]
 2: 15       F
 3: 57       Z Loc [ TOT_15mi  ]


43:  Spatial Average (P51)
 1: 30       Swath
 2: 21       First Loc [ LLUV_1mi  ]
 3: 54       Avg Loc [ PROM_30mi ]

44:  Z=X*F (P37)
 1: 54       X Loc [ PROM_30mi ]
 2: 30       F
 3: 58       Z Loc [ TOT_30mi  ]

;*******************************************
;Se usara el valor umbral en la velocidad minima para considerar casos de viento,
;redondeandolo a la decima superior mas proxima.
45:  If (X<=>F) (P89)
 1: 15       X Loc [ VCORR_m_s ]
 2: 3        >=
 3: 0.55     F
 4: 30       Then Do

46:  Do (P86)
 1: 1        Call Subroutine 1

47:  End (P95)
;***************************************

48:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 10       Set Output Flag High (Flag 0)

49:  Set Active Storage Area (P80)
 1: 3        Input Storage Area
 2: 59       Loc [ RUN_AVER  ]

50:  Average (P71)^15645
 1: 1        Reps
 2: 15       Loc [ VCORR_m_s ]



51:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 30       Then Do

     52:  Spatial Maximum (P49)
      1: 8        Swath
      2: 1        First Loc [ NORTE     ]
      3: 1060     Max Loc [ FREC_VIEN ]

     53:  CASE (P93)
      1: 60       Case Loc [ FREC_VIEN ]


          54:  If Case Location < F (P83)
           1: 121      F
           2: 30       Then Do


               55:  If (X<=>F) (P89)
                1: 59       X Loc [ RUN_AVER  ]
                2: 4        <
                3: 1.00001  F
                4: 30       Then Do

                    56:  Z=F x 10^n (P30)
                     1: 0        F
                     2: 0        n, Exponent of 10
                     3: 61       Z Loc [ DIR_PRED  ]

               57:  Else (P94)

                    58:  Z=F x 10^n (P30)
                     1: 9        F
                     2: 0        n, Exponent of 10
                     3: 61       Z Loc [ DIR_PRED  ]

               59:  End (P95)

          60:  End (P95)

     61:  End (P95)

     62:  CASE (P93)
      1: 60       Case Loc [ FREC_VIEN ]

          63:  If Case Location < F (P83)
           1: 361      F
           2: 30       Then Do

               64:  If (X<=>F) (P89)
                1: 59       X Loc [ RUN_AVER  ]
                2: 4        <
                3: 1.00001  F
                4: 30       Then Do

                    65:  Z=F x 10^n (P30)
                     1: 0        F
                     2: 0        n, Exponent of 10
                     3: 61       Z Loc [ DIR_PRED  ]

               66:  End (P95)

          67:  End (P95)

     68:  End (P95)

     69:  Beginning of Loop (P87)
      1: 0        Delay
      2: 8        Loop Count

          70:  Z=F x 10^n (P30)
           1: 0        F
           2: 0        n, Exponent of 10
           3: 1     -- Z Loc [ NORTE     ]

     71:  End (P95)

     72:  Z=X*F (P37)
      1: 66       X Loc [ CASOS_sol ]
      2: 0.1667   F
      3: 67       Z Loc [ SOL_min   ]

     73:  Z=INT(X) (P45)
      1: 67       X Loc [ SOL_min   ]
      2: 68       Z Loc [ INT_sol   ]

     74:  Z=F x 10^n (P30)
      1: 0        F
      2: 0        n, Exponent of 10
      3: 66       Z Loc [ CASOS_sol ]

75:  End (P95)

;*****CONTROL DE CALIDAD DEL DATO PARA TEMPERATURA******


76:  Z=X (P31)
 1: 9        X Loc [ TEMP_C    ]
 2: 72       Z Loc [ TEMP_C2   ]

77:  Block Move (P54)
 1: 2        No. of Values
 2: 71       First Source Loc [ temp_2    ]
 3: 1        Source Step
 4: 70       First Destination Loc [ temp_1    ]
 5: 1        Destination Step


78:  Z=X-Y (P35)
 1: 70       X Loc [ temp_1    ]
 2: 71       Y Loc [ temp_2    ]
 3: 73       Z Loc [ Dif_TempC ]

79:  Z=ABS(X) (P43)
 1: 73       X Loc [ Dif_TempC ]
 2: 73       Z Loc [ Dif_TempC ]

80:  If (X<=>F) (P89)
 1: 73       X Loc [ Dif_TempC ]
 2: 3        >=
 3: 2        F
 4: 30       Then Do

     81:  Z=Z+1 (P32)
      1: 74       Z Loc [ out_ins_T ]

     82:  If (X<=>F) (P89)
      1: 74       X Loc [ out_ins_T ]
      2: 3        >=
      3: 119      F
      4: 30       Then Do

          83:  Z=F x 10^n (P30)
           1: -8       F
           2: 0        n, Exponent of 10
           3: 76       Z Loc [ FLG_ins_T ]

     84:  Else (P94)

          85:  Z=F x 10^n (P30)
           1: 0        F
           2: 0        n, Exponent of 10
           3: 76       Z Loc [ FLG_ins_T ]


     86:  End (P95)

87:  End (P95)
;///////Promedio temp ultima hora///////

88:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 10       Set Output Flag High (Flag 0)

89:  Set Active Storage Area (P80)
 1: 3        Input Storage Area
 2: 83       Loc [ TempC_H   ]

90:  Average (P71)^11974
 1: 2        Reps
 2: 9        Loc [ TEMP_C    ]
91:  CASE (P93)
 1: 83       Case Loc [ TempC_H   ]

;*****falta poner los parametros de Santa Barbara*****
     92:  If Case Location < F (P83)
      1: 20       F
      2: 30       Then Do

          93:  Z=F x 10^n (P30)
           1: -1       F
           2: 0        n, Exponent of 10
           3: 75       Z Loc [ TOUT_flag ]

     94:  End (P95)

     95:  If Case Location < F (P83)
      1: 21       F
      2: 30       Then Do

          96:  Z=F x 10^n (P30)
           1: 0        F
           2: 0        n, Exponent of 10
           3: 75       Z Loc [ TOUT_flag ]

     97:  Else (P94)

          98:  Z=F x 10^n (P30)
           1: 1        F
           2: 0        n, Exponent of 10
           3: 75       Z Loc [ TOUT_flag ]

     99:  End (P95)

100:  End (P95)

;////////////DIP TEST EN OPERACION//////////
101:  If (X<=>F) (P89)
 1: 83       X Loc [ TempC_H   ]
 2: 4        <
 3: 0        F
 4: 30       Then Do

     102:  Z=F x 10^n (P30)
      1: -9       F
      2: 0        n, Exponent of 10
      3: 83       Z Loc [ TempC_H   ]

103:  End (P95)




104:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 30       Then Do


105:  Z=X (P31)
 1: 83       X Loc [ TempC_H   ]
 2: 88       Z Loc [ temer_4   ]

106:  Z=X (P31)
 1: 84       X Loc [ HR_H      ]
 2: 92       Z Loc [ HR_4H     ]

107:  Block Move (P54)
 1: 3        No. of Values
 2: 86       First Source Loc [ temer_2   ]
 3: 1        Source Step
 4: 85       First Destination Loc [ temer_1   ]
 5: 1        Destination Step


108:  Block Move (P54)
 1: 3        No. of Values
 2: 90       First Source Loc [ HR_2H     ]
 3: 1        Source Step
 4: 89       First Destination Loc [ HR_1H     ]
 5: 1        Destination Step


109:  Z=X-Y (P35)
 1: 85       X Loc [ temer_1   ]
 2: 86       Y Loc [ temer_2   ]
 3: 93       Z Loc [ Timenos1  ]

110:  Z=X-Y (P35)
 1: 87       X Loc [ temer_3   ]
 2: 86       Y Loc [ temer_2   ]
 3: 94       Z Loc [ Timas     ]

111:  Z=X*Y (P36)
 1: 93       X Loc [ Timenos1  ]
 2: 94       Y Loc [ Timas     ]
 3: 95       Z Loc [ delt_calc ]

112:  CASE (P93)
 1: 95       Case Loc [ delt_calc ]

     113:  If Case Location < F (P83)
      1: 4        F
      2: 30       Then Do

     114:  Z=F x 10^n (P30)
      1: 0        F
      2: 0        n, Exponent of 10
      3: 79       Z Loc [ delta_err ]


115:  Else (P94)

     116:  Z=F x 10^n (P30)
      1: -2       F
      2: 0        n, Exponent of 10
      3: 79       Z Loc [ delta_err ]

117:  End (P95)

118:  End (P95)

;//////Diferencia con respecto a la hora anterior///////


119:  Z=X-Y (P35)
 1: 87       X Loc [ temer_3   ]
 2: 86       Y Loc [ temer_2   ]
 3: 96       Z Loc [ JUMP_2C   ]

120:  Z=ABS(X) (P43)
 1: 96       X Loc [ JUMP_2C   ]
 2: 96       Z Loc [ JUMP_2C   ]

121:  If (X<=>F) (P89)
 1: 96       X Loc [ JUMP_2C   ]
 2: 3        >=
 3: 2        F
 4: 30       Then Do

     122:  Z=F x 10^n (P30)
      1: -5       F
      2: 0        n, Exponent of 10
      3: 78       Z Loc [ Flag_Jump ]

123:  Else (P94)

     124:  Z=F x 10^n (P30)
      1: 0        F
      2: 0        n, Exponent of 10
      3: 78       Z Loc [ Flag_Jump ]


125:  End (P95)

;////Razon de cambio de la temperatura con la HR//////


126:  Z=X-Y (P35)
 1: 87       X Loc [ temer_3   ]
 2: 86       Y Loc [ temer_2   ]
 3: 97       Z Loc [ T24_T23   ]


127:  Z=X-Y (P35)
 1: 91       X Loc [ HR_3H     ]
 2: 90       Y Loc [ HR_2H     ]
 3: 98       Z Loc [ HR3_HR2   ]


128:  Z=X/Y (P38)
 1: 97       X Loc [ T24_T23   ]
 2: 98       Y Loc [ HR3_HR2   ]
 3: 99       Z Loc [ T_HR_cstc ]

129:  If (X<=>F) (P89)
 1: 99       X Loc [ T_HR_cstc ]
 2: 4        <
 3: 0        F
 4: 30       Then Do

     130:  Z=F x 10^n (P30)
      1: 0        F
      2: 0        n, Exponent of 10
      3: 77       Z Loc [ THR_flag  ]

131:  Else (P94)

     132:  Z=F x 10^n (P30)
      1: -4       F
      2: 0        n, Exponent of 10
      3: 77       Z Loc [ THR_flag  ]

133:  End (P95)

134:  End (P95)

;/////TEMPERATURAS ABSOLUTAS MAXIMAS Y MINIMAS////////


135:  If time is (P92)
 1: 419      Minutes (Seconds --) into a
 2: 1440     Interval (same units as above)
 3: 10       Set Output Flag High (Flag 0)


136:  Set Active Storage Area (P80)
 1: 3        Input Storage Area
 2: 100      Loc [ TmaxD_abs ]

137:  Maximum (P73)^2616
 1: 2        Reps
 2: 10       Value with Hr-Min
 3: 9        Loc [ TEMP_C    ]

138:  If time is (P92)
 1: 419      Minutes (Seconds --) into a
 2: 1440     Interval (same units as above)
 3: 10       Set Output Flag High (Flag 0)

139:  Set Active Storage Area (P80)
 1: 3        Input Storage Area
 2: 102      Loc [ TminD_abs ]

140:  Minimum (P74)^7582
 1: 2        Reps
 2: 10       Value with Hr-Min
 3: 9        Loc [ TEMP_C    ]

;/////Subrutinas para T maxima y T minima///////

141:  If time is (P92)
 1: 419      Minutes (Seconds --) into a
 2: 1440     Interval (same units as above)
 3: 2        Call Subroutine 2

142:  If time is (P92)
 1: 419      Minutes (Seconds --) into a
 2: 1440     Interval (same units as above)
 3: 3        Call Subroutine 3

;///////////////////////////////////////////////


143:  If time is (P92)
 1: 419      Minutes (Seconds --) into a
 2: 1440     Interval (same units as above)
 3: 30       Then Do

144:  If (X<=>Y) (P88)
 1: 102      X Loc [ TminD_abs ]
 2: 3        >=
 3: 100      Y Loc [ TmaxD_abs ]
 4: 30       Then Do

     145:  Z=F x 10^n (P30)
      1: -3       F
      2: 0        n, Exponent of 10
      3: 82       Z Loc [ FlagT_day ]

146:  Else (P94)

     147:  Z=F x 10^n (P30)
      1: 0        F
      2: 0        n, Exponent of 10
      3: 82       Z Loc [ FlagT_day ]

148:  End (P95)

149:  End (P95)




;***********    SALIDA HORARIA:  **************
;**********************************************

150:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 10       Set Output Flag High (Flag 0)

151:  Set Active Storage Area (P80)^4337
 1: 1        Final Storage Area 1
 2: 1        Array ID

152:  Resolution (P78)
 1: 1        High Resolution


153:  Sample (P70)^21376
 1: 1        Reps
 2: 20       Loc [ ESTACION  ]

154:  Resolution (P78)
 1: 0        Low Resolution

155:  Real Time (P77)^25644
 1: 1220     Year,Day,Hour/Minute (midnight = 2400)

156:  Average (P71)^11746
 1: 1        Reps
 2: 9        Loc [ TEMP_C    ]

157:  Average (P71)^18710
 1: 1        Reps
 2: 10       Loc [ HR        ]

158:  Maximum (P73)^12841
 1: 1        Reps
 2: 0        Value Only
 3: 11       Loc [ RAD_KW_m2 ]

159:  Minimum (P74)^15125
 1: 1        Reps
 2: 0        Value Only
 3: 11       Loc [ RAD_KW_m2 ]

160:  Totalize (P72)^8599
 1: 1        Reps
 2: 13       Loc [ RAD_MJ_m2 ]

161:  Sample (P70)^22669
 1: 1        Reps
 2: 68       Loc [ INT_sol   ]
;///////////////////////////////////////////
162:  Sample (P70)^22669
 1: 1        Reps
 2: 165      Loc [ llv_T1h   ]
;///////////////////////////////////////////
163:  Sample (P70)^16495
 1: 2        Reps
 2: 60       Loc [ FREC_VIEN ]

164:  Wind Vector (P69)^6749
 1: 1        Reps
 2: 90       Samples per Sub-Interval
 3: 2        S, U, theta(u), sigma(theta(u)) with polar sensor
 4: 15       Wind Speed/East Loc [ VCORR_m_s ]
 5: 16       Wind Direction/North Loc [ DIR_vient ]

;///////salida extra de control de calidad para temperatura/////////


165:  Sample (P70)^28304
 1: 7        Reps
 2: 73       Loc [ Dif_TempC ]

166:  Standard Deviation (P82)^28165
 1: 1        Reps
 2: 9        Sample Loc [ TEMP_C    ]
;///////salida extra de control de calidad para lluvia/////////


167:  Sample (P70)^5355
 1: 5        Reps
 2: 160      Loc [ FLG_CUSUM ]


168:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 30       Then Do

     169:  Z=F x 10^n (P30)
      1: 0        F
      2: 0        n, Exponent of 10
      3: 74       Z Loc [ out_ins_T ]

     170:  Z=F x 10^n (P30)
      1: 0        F
      2: 0        n, Exponent of 10
      3: 76       Z Loc [ FLG_ins_T ]

     171:  Z=F x 10^n (P30)
      1: 0        F
      2: 00       n, Exponent of 10
      3: 162      Z Loc [ COUNTER   ]

     172:  Z=F x 10^n (P30)
      1: 0        F
      2: 0        n, Exponent of 10
      3: 161      Z Loc [ FLAG_INT  ]

     173:  Z=F x 10^n (P30)
      1: 0        F
      2: 0        n, Exponent of 10
      3: 163      Z Loc [ SEC_FLAG  ]

     174:  Z=F x 10^n (P30)
      1: 0        F
      2: 0        n, Exponent of 10
      3: 160      Z Loc [ FLG_CUSUM ]

175:  End (P95)


;******   SALIDA  DIARIA **********************************


176:  If time is (P92)
 1: 420      Minutes (Seconds --) into a
 2: 1440     Interval (same units as above)
 3: 10       Set Output Flag High (Flag 0)

177:  Set Active Storage Area (P80)^30893
 1: 1        Final Storage Area 1
 2: 2        Array ID

178:  Resolution (P78)
 1: 1        High Resolution

179:  Sample (P70)^22448
 1: 1        Reps
 2: 20       Loc [ ESTACION  ]

180:  Resolution (P78)
 1: 0        Low Resolution

181:  Real Time (P77)^15377
 1: 1220     Year,Day,Hour/Minute (midnight = 2400)

182:  Maximum (P73)^4028
 1: 2        Reps
 2: 10       Value with Hr-Min
 3: 9        Loc [ TEMP_C    ]

183:  Minimum (P74)^31645
 1: 2        Reps
 2: 10       Value with Hr-Min
 3: 9        Loc [ TEMP_C    ]

184:  Maximum (P73)^16041
 1: 1        Reps
 2: 10       Value with Hr-Min
 3: 15       Loc [ VCORR_m_s ]

185:  Maximum (P73)^20506
 1: 4        Reps
 2: 0        Value Only
 3: 55       Loc [ TOT_5mi   ]

186:  Wind Vector (P69)^26011
 1: 1        Reps
 2: 360      Samples per Sub-Interval
 3: 2        S, U, theta(u), sigma(theta(u)) with polar sensor
 4: 15       Wind Speed/East Loc [ VCORR_m_s ]
 5: 16       Wind Direction/North Loc [ DIR_vient ]

187:  Sample (P70)^6571
 1: 3        Reps
 2: 80       Loc [ FL_Tmin   ]

;******** SALIDA DE CONTROL A LAS 4AM ******************************

188:  If time is (P92)
 1: 240      Minutes (Seconds --) into a
 2: 1440     Interval (same units as above)
 3: 10       Set Output Flag High (Flag 0)

189:  Set Active Storage Area (P80)^18387
 1: 1        Final Storage Area 1
 2: 3        Array ID

190:  Real Time (P77)^26279
 1: 220      Day,Hour/Minute (midnight = 2400)


191:  Sample (P70)^19064
 1: 2        Reps
 2: 18       Loc [ TEM_LOG   ]

192:  Maximum (P73)^1017
 1: 1        Reps
 2: 00       Time Option
 3: 64       Loc [ HR_caja   ]

193:  Z=F x 10^n (P30)
 1: 0        F
 2: 00       n, Exponent of 10
 3: 69       Z Loc [ CONTR_SEN ]

194:  Sample (P70)^31330
 1: 1        Reps
 2: 69       Loc [ CONTR_SEN ]


195:  Serial Out (P96)
 1: 71       Storage Module

*Table 2 Program
  02: 1         Execution Interval (seconds)

1:  Pulse (P3)
 1: 1        Reps
 2: 1        Pulse Channel 1
 3: 2        Switch Closure, All Counts
 4: 104      Loc [ Tips      ]
 5: 1        Mult
 6: 0        Offset

2:  Z=X*F (P37)
 1: 104      X Loc [ Tips      ]
 2: 0.1      F
 3: 17       Z Loc [ LLUV_mm   ]


3:  If (X<=>F) (P89)
 1: 104      X Loc [ Tips      ]
 2: 3        >=
 3: 1        F
 4: 30       Then Do

     4:  Time (P18)
      1: 3        Store Year,Day,Hr,Min,Sec in 5 consecutive locations
      2: 0        Mod/By
      3: 105      Loc [ Tp_Year   ]


     5:  Z=X*F (P37)
      1: 107      X Loc [ Tp_Hour   ]
      2: 3600     F
      3: 110      Z Loc [ Tp_Hour_S ]


     6:  Z=X*F (P37)
      1: 108      X Loc [ Tp_Min    ]
      2: 60       F
      3: 111      Z Loc [ Tp_Min_S  ]

     7:  Z=X+Y (P33)
      1: 111      X Loc [ Tp_Min_S  ]
      2: 110      Y Loc [ Tp_Hour_S ]
      3: 124      Z Loc [ Time_13   ]

     8:  Z=X+Y (P33)
      1: 124      X Loc [ Time_13   ]
      2: 109      Y Loc [ Tp_Sec    ]
      3: 124      Z Loc [ Time_13   ]

     9:  Block Move (P54)
      1: 12       No. of Values
      2: 113      First Source Loc [ Time_2    ]
      3: 1        Source Step
      4: 112      First Destination Loc [ Time_1    ]
      5: 1        Destination Step


     10:  Z=X-Y (P35)
      1: 123      X Loc [ Time_12   ]
      2: 122      Y Loc [ Time_11   ]
      3: 137      Z Loc [ interT_13 ]


     11:  If (X<=>F) (P89)
      1: 137      X Loc [ interT_13 ]
      2: 4        <
      3: 0        F
      4: 30       Then Do

          12:  Z=F x 10^n (P30)
           1: 86400    F
           2: 0        n, Exponent of 10
           3: 138      Z Loc [ S86400    ]

          13:  Z=X-Y (P35)
           1: 138      X Loc [ S86400    ]
           2: 122      Y Loc [ Time_11   ]
           3: 137      Z Loc [ interT_13 ]

          14:  Z=X+Y (P33)
           1: 137      X Loc [ interT_13 ]
           2: 124      Y Loc [ Time_13   ]
           3: 137      Z Loc [ interT_13 ]

     15:  End (P95)

     16:  Block Move (P54)
      1: 12       No. of Values
      2: 126      First Source Loc [ interTT_2 ]
      3: 1        Source Step
      4: 125      First Destination Loc [ interTT_1 ]
      5: 1        Destination Step


     17:  Z=X/Y (P38)
      1: 136      X Loc [ interT_12 ]
      2: 135      Y Loc [ interT_11 ]
      3: 139      Z Loc [ TAU       ]

     18:  Z=LN(X) (P40)
      1: 139      X Loc [ TAU       ]
      2: 159      Z Loc [ LAMBDA    ]

     19:  Z=ABS(X) (P43)
      1: 159      X Loc [ LAMBDA    ]
      2: 159      Z Loc [ LAMBDA    ]

20:  End (P95)

21:  If (X<=>F) (P89)
 1: 159      X Loc [ LAMBDA    ]
 2: 3        >=
 3: 3        F
 4: 30       Then Do

     22:  Z=X (P31)
      1: 159      X Loc [ LAMBDA    ]
      2: 140      Z Loc [ LAMBD_OUT ]

     23:  Z=Z+1 (P32)
      1: 162      Z Loc [ COUNTER   ]

     24:  Z=F x 10^n (P30)
      1: 0        F
      2: 0        n, Exponent of 10
      3: 159      Z Loc [ LAMBDA    ]


25:  End (P95)
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;:::::::::CONTROLES DE CALIDAD CONOCIDO EL TIEMPO ENTRE TIPS::::::::::::::::::::::::::::::::::::::

26:  If (X<=>F) (P89)
 1: 104      X Loc [ Tips      ]
 2: 3        >=
 3: 0.1      F
 4: 30       Then Do


          27:  If (X<=>F) (P89)
           1: 137      X Loc [ interT_13 ]
           2: 4        <
           3: 3        F
           4: 30       Then Do

               28:  Z=Z+1 (P32)
                1: 161      Z Loc [ FLAG_INT  ]

          29:  End (P95)

     30:  End (P95)

     31:  If (X<=>F) (P89)
      1: 104      X Loc [ Tips      ]
      2: 3        >=
      3: 0.1      F
      4: 30       Then Do

          32:  If (X<=>Y) (P88)
           1: 135      X Loc [ interT_11 ]
           2: 4        <
           3: 137      Y Loc [ interT_13 ]
           4: 30       Then Do

               33:  Z=F x 10^n (P30)
                1: 1        F
                2: 0        n, Exponent of 10
                3: 153      Z Loc [ CHNG_INDX ]

          34:  End (P95)

          35:  If (X<=>Y) (P88)
           1: 137      X Loc [ interT_13 ]
           2: 4        <
           3: 135      Y Loc [ interT_11 ]
           4: 30       Then Do

               36:  Z=F x 10^n (P30)
                1: -1       F
                2: 0        n, Exponent of 10
                3: 153      Z Loc [ CHNG_INDX ]

          37:  End (P95)

          38:  If (X<=>Y) (P88)
           1: 137      X Loc [ interT_13 ]
           2: 1        =
           3: 135      Y Loc [ interT_11 ]
           4: 30       Then Do

               39:  Z=F x 10^n (P30)
                1: 0        F
                2: 0        n, Exponent of 10
                3: 153      Z Loc [ CHNG_INDX ]

          40:  End (P95)

          41:  Block Move (P54)
           1: 12       No. of Values
           2: 142      First Source Loc [ CHG_2     ]
           3: 1        Source Step
           4: 141      First Destination Loc [ CHG_1     ]
           5: 1        Destination Step

          42:  Spatial Average (P51)
           1: 12       Swath
           2: 141      First Loc [ CHG_1     ]
           3: 154      Avg Loc [ SECUENCE  ]

          43:  Z=X (P31)
           1: 154      X Loc [ SECUENCE  ]
           2: 155      Z Loc [ SEC_VER   ]

          44:  If (X<=>F) (P89)
           1: 155      X Loc [ SEC_VER   ]
           2: 1        =
           3: 1        F
           4: 30       Then Do

               45:  Z=Z+1 (P32)
                1: 163      Z Loc [ SEC_FLAG  ]

               46:  Z=F x 10^n (P30)
                1: 0        F
                2: 0        n, Exponent of 10
                3: 154      Z Loc [ SECUENCE  ]

          47:  End (P95)

;**********ALGORITMO DE CUSUM PARA DETECCION DE PLUVIOMETROS BLOQUEADOS****************

          48:  Z=X+Y (P33)
           1: 156      X Loc [ CUSUM     ]
           2: 153      Y Loc [ CHNG_INDX ]
           3: 156      Z Loc [ CUSUM     ]


          49:  Z=Z+1 (P32)
           1: 157      Z Loc [ Nk        ]

          50:  Z=X/Y (P38)
           1: 156      X Loc [ CUSUM     ]
           2: 157      Y Loc [ Nk        ]
           3: 158      Z Loc [ rk        ]

     51:  End (P95)

     52:  If (X<=>F) (P89)
      1: 156      X Loc [ CUSUM     ]
      2: 1        =
      3: 12       F
      4: 30       Then Do

          53:  If (X<=>F) (P89)
           1: 158      X Loc [ rk        ]
           2: 3        >=
           3: 0.1      F
           4: 30       Then Do

               54:  Z=F x 10^n (P30)
                1: 5        F
                2: 0        n, Exponent of 10
                3: 160      Z Loc [ FLG_CUSUM ]

          55:  End (P95)

          56:  If (X<=>F) (P89)
           1: 137      X Loc [ interT_13 ]
           2: 3        >=
           3: 7200     F
           4: 30       Then Do

               57:  Z=F x 10^n (P30)
                1: 0        F
                2: 0        n, Exponent of 10
                3: 156      Z Loc [ CUSUM     ]

               58:  Z=F x 10^n (P30)
                1: 0        F
                2: 0        n, Exponent of 10
                3: 158      Z Loc [ rk        ]

          59:  End (P95)

     60:  End (P95)


;/////Algoritmo para cuando el total horario de lluvia supera el climatico/////

     61:  If time is (P92)
      1: 59       Minutes (Seconds --) into a
      2: 60       Interval (same units as above)
      3: 10       Set Output Flag High (Flag 0)

     62:  Set Active Storage Area (P80)
      1: 3        Input Storage Area
      2: 165      Loc [ llv_T1h   ]

     63:  Totalize (P72)^4137
      1: 1        Reps
      2: 17       Loc [ LLUV_mm   ]

     64:  If (X<=>F) (P89)
      1: 165      X Loc [ llv_T1h   ]
      2: 3        >=
      3: 1        F
      4: 30       Then Do


          65:  Z=F x 10^n (P30)
           1: -10      F
           2: 0        n, Exponent of 10
           3: 164      Z Loc [ FL_llvT1h ]

     66:  Else (P94)

          67:  Z=F x 10^n (P30)
           1: 0        F
           2: 0        n, Exponent of 10
           3: 164      Z Loc [ FL_llvT1h ]


     68:  End (P95)


*Table 3 Subroutines


1:  Beginning of Subroutine (P85)
 1: 1        Subroutine 1

     2:  CASE (P93)
      1: 16       Case Loc [ DIR_vient ]

          3:  If Case Location < F (P83)
           1: 22.5     F
           2: 30       Then Do

               4:  Z=Z+1 (P32)
                1: 1        Z Loc [ NORTE     ]

          5:  End (P95)

          6:  If Case Location < F (P83)
           1: 67.5     F
           2: 30       Then Do

               7:  Z=Z+1 (P32)
                1: 2        Z Loc [ NORESTE   ]

          8:  End (P95)

          9:  If Case Location < F (P83)
           1: 112.5    F
           2: 30       Then Do

               10:  Z=Z+1 (P32)
                1: 3        Z Loc [ ESTE      ]

          11:  End (P95)

          12:  If Case Location < F (P83)
           1: 157.5    F
           2: 30       Then Do

               13:  Z=Z+1 (P32)
                1: 4        Z Loc [ SURESTE   ]

          14:  End (P95)

          15:  If Case Location < F (P83)
           1: 202.5    F
           2: 30       Then Do

               16:  Z=Z+1 (P32)
                1: 5        Z Loc [ SUR       ]

          17:  End (P95)

          18:  If Case Location < F (P83)
           1: 247.5    F
           2: 30       Then Do

               19:  Z=Z+1 (P32)
                1: 6        Z Loc [ SUROESTE  ]

          20:  End (P95)

          21:  If Case Location < F (P83)
           1: 292.5    F
           2: 30       Then Do

               22:  Z=Z+1 (P32)
                1: 7        Z Loc [ OESTE     ]

          23:  End (P95)

          24:  If Case Location < F (P83)
           1: 337.5    F
           2: 30       Then Do

               25:  Z=Z+1 (P32)
                1: 8        Z Loc [ NOROESTE  ]

          26:  End (P95)

          27:  If Case Location < F (P83)
           1: 361      F
           2: 30       Then Do

               28:  Z=Z+1 (P32)
                1: 1        Z Loc [ NORTE     ]

          29:  End (P95)

     30:  End (P95)

31:  End (P95)


;/////////T maxima  dia/////////////
32:  Beginning of Subroutine (P85)
 1: 2        Subroutine 2

33:  CASE (P93)
 1: 101      Case Loc [ HM_Tmax   ]

34:  If Case Location < F (P83)
 1: 900      F
 2: 30       Then Do
     35:  Z=F x 10^n (P30)
      1: -6       F
      2: 0        n, Exponent of 10
      3: 81       Z Loc [ FL_Tmax   ]

36:  End (P95)

37:  If Case Location < F (P83)
 1: 1500     F
 2: 30       Then Do

     38:  Z=F x 10^n (P30)
      1: 0        F
      2: 0        n, Exponent of 10
      3: 81       Z Loc [ FL_Tmax   ]

39:  End (P95)
40:  If Case Location < F (P83)
 1: 2400     F
 2: 30       Then Do

     41:  Z=F x 10^n (P30)
      1: -6       F
      2: 0        n, Exponent of 10
      3: 81       Z Loc [ FL_Tmax   ]

42:  End (P95)


43:  End (P95)

44:  End (P95)

;//////////T minima  dia/////////////

45:  Beginning of Subroutine (P85)
 1: 3        Subroutine 3

46:  CASE (P93)
 1: 103      Case Loc [ HM_Tmin   ]

47:  If Case Location < F (P83)
 1: 600      F
 2: 30       Then Do
     48:  Z=F x 10^n (P30)
      1: 0        F
      2: 0        n, Exponent of 10
      3: 80       Z Loc [ FL_Tmin   ]

49:  End (P95)

50:  If Case Location < F (P83)
 1: 2300     F
 2: 30       Then Do

     51:  Z=F x 10^n (P30)
      1: -7       F
      2: 0        n, Exponent of 10
      3: 80       Z Loc [ FL_Tmin   ]

52:  End (P95)

53:  If Case Location < F (P83)
 1: 2400     F
 2: 30       Then Do
     54:  Z=F x 10^n (P30)
      1: 0        F
      2: 0        n, Exponent of 10
      3: 80       Z Loc [ FL_Tmin   ]

55:  End (P95)

56:  End (P95)

57:  End (P95)

End Program

-Input Locations-
1 NORTE     1 1 3
2 NORESTE   1 0 1
3 ESTE      1 0 1
4 SURESTE   1 0 0
5 SUR       1 0 1
6 SUROESTE  1 0 1
7 OESTE     1 0 1
8 NOROESTE  1 0 1
9 TEMP_C    1 8 1
10 HR        1 9 1
11 RAD_KW_m2 1 5 2
12 RAD_KJ_m2 1 2 4
13 RAD_MJ_m2 1 2 2
14 VEL_m_s   1 2 1
15 VCORR_m_s 1 5 2
16 DIR_vient 1 3 1
17 LLUV_mm   1 2 1
18 TEM_LOG   1 1 1
19 VOLT_BAT  1 0 1
20 ESTACION  1 3 1
21 LLUV_1mi  5 1 1
22 LLUV_2mi  9 2 1
23 LLUV_3mi  9 2 1
24 LLUV_4mi  9 2 1
25 LLUV_5mi  9 2 1
26 LLUV_6mi  9 2 1
27 LLUV_7mi  9 2 1
28 LLUV_8mi  9 2 1
29 LLUV_9mi  9 2 1
30 LLUV_10mi 9 2 1
31 LLUV_11mi 9 2 1
32 LLUV_12mi 9 2 1
33 LLUV_13mi 9 2 1
34 LLUV_14mi 9 2 1
35 LLUV_15mi 9 2 1
36 LLUV_16mi 9 3 1
37 LLUV_17mi 9 3 1
38 LLUV_18mi 9 3 1
39 LLUV_19mi 9 3 1
40 LLUV_20mi 9 3 1
41 LLUV_21mi 9 4 1
42 LLUV_22mi 9 4 1
43 LLUV_23mi 9 4 1
44 LLUV_24mi 9 4 1
45 LLUV_25mi 9 4 1
46 LLUV_26mi 9 5 1
47 LLUV_27mi 9 5 1
48 LLUV_28mi 9 5 1
49 LLUV_29mi 17 5 1
50 LLUV_30mi 1 4 1
51 PRO_5mi   1 1 1
52 PRO_10mi  1 3 0
53 PRO_15mi  1 2 1
54 PROM_30mi 1 2 1
55 TOT_5mi   1 2 1
56 TOT_10mi  1 1 1
57 TOT_15mi  1 1 1
58 TOT_30mi  1 1 1
59 RUN_AVER  1 1 1
60 FREC_VIEN 5 2 1
61 DIR_PRED  17 0 4
62 TIME_NUM  1 1 1
63 PRES_mb   1 0 0
64 HR_caja   1 0 1
65 SENS_AUS  1 0 1
66 CASOS_sol 1 0 2
67 SOL_min   1 0 1
68 INT_sol   1 1 1
69 CONTR_SEN 1 1 1
70 temp_1    7 1 1
71 temp_2    19 2 1
72 TEMP_C2   1 1 1
73 Dif_TempC 1 3 2
74 out_ins_T 1 2 2
75 TOUT_flag 1 1 3
76 FLG_ins_T 1 1 3
77 THR_flag  1 1 2
78 Flag_Jump 1 1 2
79 delta_err 1 0 2
80 FL_Tmin   1 1 3
81 FL_Tmax   1 1 3
82 FlagT_day 1 1 2
83 TempC_H   7 3 2
84 HR_H      19 1 0
85 temer_1   7 1 1
86 temer_2   11 5 1
87 temer_3   19 4 1
88 temer_4   1 1 1
89 HR_1H     7 0 1
90 HR_2H     11 2 1
91 HR_3H     19 2 1
92 HR_4H     1 1 1
93 Timenos1  1 1 1
94 Timas     1 1 1
95 delt_calc 1 1 1
96 JUMP_2C   1 2 2
97 T24_T23   1 1 1
98 HR3_HR2   1 1 1
99 T_HR_cstc 1 1 1
100 TmaxD_abs 7 1 1
101 HM_Tmax   11 1 0
102 TminD_abs 11 1 1
103 HM_Tmin   19 1 0
104 Tips      1 4 1
105 Tp_Year   5 0 1
106 Tp_Day    9 0 1
107 Tp_Hour   9 1 1
108 Tp_Min    9 1 1
109 Tp_Sec    17 1 1
110 Tp_Hour_S 1 1 1
111 Tp_Min_S  1 1 1
112 Time_1    7 0 1
113 Time_2    11 1 1
114 Time_3    11 1 1
115 Time_4    11 1 1
116 Time_5    11 1 1
117 Time_6    11 1 1
118 Time_7    11 1 1
119 Time_8    11 1 1
120 Time_9    11 1 1
121 Time_10   11 1 1
122 Time_11   11 3 1
123 Time_12   19 2 1
124 Time_13   1 3 2
125 interTT_1 7 0 1
126 interTT_2 11 1 1
127 interTT_3 11 1 1
128 interTT_4 11 1 1
129 interTT_5 11 1 1
130 interTT_6 11 1 1
131 interTT_7 11 1 1
132 interTT_8 11 1 1
133 interTT_9 11 1 1
134 interT_10 11 1 1
135 interT_11 11 5 1
136 interT_12 19 2 1
137 interT_13 1 8 3
138 S86400    1 1 1
139 TAU       1 1 1
140 LAMBD_OUT 1 0 1
141 CHG_1     7 1 1
142 CHG_2     11 2 1
143 CHG_3     11 2 1
144 CHG_4     11 2 1
145 CHG_5     11 2 1
146 CHG_6     11 2 1
147 CHG_7     11 2 1
148 CHG_8     11 2 1
149 CHG_9     11 2 1
150 CHG_10    11 2 1
151 CHG_11    11 2 1
152 CHG_12    19 2 1
153 CHNG_INDX 1 2 3
154 SECUENCE  1 1 2
155 SEC_VER   1 1 1
156 CUSUM     1 3 2
157 Nk        1 1 1
158 rk        1 1 2
159 LAMBDA    1 3 3
160 FLG_CUSUM 1 1 2
161 FLAG_INT  1 1 2
162 COUNTER   1 1 2
163 SEC_FLAG  1 1 2
164 FL_llvT1h 1 0 2
165 llv_T1h   1 2 1
-Program Security-
0000
0000
0000
-Mode 4-
-Final Storage Area 2-
0
-CR10X ID-
0
-CR10X Power Up-
3
-CR10X Compile Setting-
3
-CR10X RS-232 Setting-
-1
-DLD File Labels-
0
-Final Storage Labels-
0,LLUV_mm_TOT~17,11551
1,Year_RTM,25644
1,Day_RTM
1,Hour_Minute_RTM
2,TEMP_C_AVG~9,11746
3,HR_AVG~10,18710
4,RAD_KW_m2_MAX~11,12841
5,RAD_KW_m2_MIN~11,15125
6,RAD_MJ_m2_TOT~13,8599
7,llv_T1h~165,22669
8,FREC_VIEN~60,16495
8,DIR_PRED~61
9,VCORR_m_s_S_WVT~15,6749
9,VCORR_m_s_U_WVT~15
9,DIR_vient_DU_WVT~16
9,DIR_vient_SDU_WVT~16
10,2,30893
11,ESTACION~20,22448
12,Year_RTM,15377
12,Day_RTM
12,Hour_Minute_RTM
13,TEMP_C_MAX~9,4028
13,TEMP_C_Hr_Min_MAX~9
13,HR_MAX~10
13,HR_Hr_Min_MAX~10
14,TEMP_C_MIN~9,31645
14,TEMP_C_Hr_Min_MIN~9
14,HR_MIN~10
14,HR_Hr_Min_MIN~10
15,VCORR_m_s_MAX~15,16041
15,VCORR_m_s_Hr_Min_MAX~15
16,TOT_5mi_MAX~55,20506
16,TOT_10mi_MAX~56
16,TOT_15mi_MAX~57
16,TOT_30mi_MAX~58
17,VCORR_m_s_S_WVT~15,26011
17,VCORR_m_s_U_WVT~15
17,DIR_vient_DU_WVT~16
17,DIR_vient_SDU_WVT~16
18,3,18387
19,Day_RTM,26279
19,Hour_Minute_RTM
20,TEM_LOG~18,19064
20,VOLT_BAT~19
21,1,4337
22,ESTACION~20,21376
23,VCORR_m_s_AVG~15,15645
24,HR_caja_MAX~64,1017
25,CONTR_SEN~69,31330
26,TEMP_C_AVG~9,11974
26,HR_AVG~10
27,TEMP_C_MAX~9,2616
27,TEMP_C_Hr_Min_MAX~9
27,HR_MAX~10
27,HR_Hr_Min_MAX~10
28,TEMP_C_MIN~9,7582
28,TEMP_C_Hr_Min_MIN~9
28,HR_MIN~10
28,HR_Hr_Min_MIN~10
29,Dif_TempC~73,28304
29,out_ins_T~74
29,TOUT_flag~75
29,FLG_ins_T~76
29,THR_flag~77
29,Flag_Jump~78
29,delta_err~79
30,FL_Tmin~80,6571
30,FL_Tmax~81
30,FlagT_day~82
31,TEMP_C_STD~9,28165
32,FLG_CUSUM~160,5355
32,FLAG_INT~161
32,COUNTER~162
32,SEC_FLAG~163
32,FL_llvT1h~164
33,LLUV_mm_TOT~17,4137
